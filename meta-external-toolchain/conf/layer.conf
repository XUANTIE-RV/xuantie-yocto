BBPATH .= ":${LAYERDIR}"
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"
BBFILE_COLLECTIONS += "external-toolchain"
BBFILE_PRIORITY_external-toolchain = "1"
BBFILE_PATTERN_external-toolchain = "^${LAYERDIR}/"

LAYERDEPENDS_external-toolchain = "core"
LAYERSERIES_COMPAT_external-toolchain = "kirkstone langdale"
LAYERDIR_external-toolchain = "${LAYERDIR}"

BBFILES += "${@' '.join('${LAYERDIR}/%s/recipes*/*/*.%s' % (layer, ext) \
               for layer in '${BBFILE_COLLECTIONS}'.split() for ext in ['bb', 'bbappend'])}"

# These will rebuild when EXTERNAL_TOOLCHAIN changes, but we don't want
# everything to rebuild in such a case.
SIGGEN_EXCLUDERECIPES_ABISAFE += "\
    gcc-external-cross \
    gcc-external-cross-${TARGET_ARCH} \
    binutils-external-cross  \
    binutils-external-cross-${TARGET_ARCH} \
"

# Needed for external_run() for PV & LICENSE in the recipes, to be able to parse
INHERIT:append = " external_global"

# Fallback default for when the tcmode isn't sourced
GCC_VERSION ?= "0.0"
